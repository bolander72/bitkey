cargo := "RUSTFLAGS='--cfg feature=\"mock-time\"' cargo --locked"

[private]
clippy *args:
  {{cargo}} clippy {{args}}

# Run the CI clippy lint commands
clippy-ci:
  just clippy -- -A deprecated -A clippy::large_enum_variant -A clippy::result_large_err -A clippy::new_without_default

# Format and lint
fmt:
  {{cargo}} fmt --all
  just clippy --fix --allow-dirty --allow-staged

# Run the CI format commands
fmt-ci:
  just fmt
  git diff --exit-code

# Must run with only one thread because the NFC generators are not thread-safe
integration:
  {{cargo}} test --all-features --test integration_test -- --test-threads 1

record-integration $UPDATE_EXPECT="1":
  {{cargo}} test --all-features --test integration_test

end-to-end $AWS_PROFILE="bitkey-development--admin" $AWS_REGION="us-west-2":
  {{cargo}} run --release -- end-to-end

uniffi-jvm:
  gradle generateKotlinRustBindingsJvm

uniffi-android:
  gradle generateKotlinRustBindingsAndroid
